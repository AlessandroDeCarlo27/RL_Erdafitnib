DESCRIPTION:

[LONGITUDINAL]
input = {tlag, ka, Cl, V2, Q3, V3, Q4, V4, fu, ke0, gamma, m, kin, PO40, PO4P, kbase, TSLD, tlag_att, q10, q20, q30, q40, T0, Ce0}

PK:

V2tot=V2
V2free=V2tot/fu
k02 =Cl/V2tot
k32= Q3/V2free    				;inter-compartment clearance
k23=Q3/V3					;inter-compartment clearance
k42=Q4/V2free					;inter-compartment clearance
k24=Q4/V4 					;inter-compartment clearance
k02=Cl/V2tot					;elimination clearance

depot(type=1, target=q1, Tlag=tlag)

EQUATION:
; PK model definition
;t0=0
q1_0=q10
q2_0=q20
q3_0=q30
q4_0=q40

ddt_q1=-ka*q1							;administration compartment
ddt_q2=ka*q1-k02*fu*q2-k32*fu*q2+k23*q3-k42*fu*q2+k24*q4	;central compartment 
ddt_q3=k32*fu*q2-k23*q3						;peripheral compartment 1 
ddt_q4=k42*fu*q2-k24*q4						;peripheral compartment 2

Ctot=q2/V2tot
Cfree=fu*Ctot

;PD model definition
Ce_0=Ce0
T_0=T0

ddt_Ce=ke0*Cfree-ke0*Ce
ddt_T=kin*(1-T)
M=m*(1-T)

if TSLD>tlag_att
	PO4_BSL=PO4P-(PO4P-PO40)*exp(-kbase*(TSLD-tlag_att))
else
	PO4_BSL=PO40
end

PO4_BSLfin=PO4_BSL

PO4=PO4_BSLfin+M*(Ce^gamma)


OUTPUT:
output={Ctot, Cfree, PO4, Ce, T, q1, q2, q3, q4}
